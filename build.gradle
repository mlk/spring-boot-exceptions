buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
}

apply plugin: 'java'
apply plugin: "info.solidsoft.pitest"
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'propdeps'

group = 'com.github.mlk'
version = "1.1.${System.env.CIRCLE_BUILD_NUM}"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


dependencies {
    provided 'org.springframework.boot:spring-boot-starter-web:1.5.9.RELEASE'
    provided 'org.springframework.boot:spring-boot-starter-actuator:1.5.9.RELEASE'

    compile 'com.google.guava:guava:27.0-jre'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.9.RELEASE'
}

uploadArchives {
    if(project.hasProperty("ossrhUsername")) {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Spring Boot Exceptions'
                    packaging 'jar'
                    description 'Simple exceptions-to-Spring Boot web Exceptions'
                    url 'https://www.github.com/mlk/spring-boot-exceptions'

                    scm {
                        connection 'scm:git:https://www.github.com/mlk/spring-boot-exceptions/'
                        url 'https://www.github.com/mlk/spring-boot-exceptions/'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://www.github.com/mlk/spring-boot-exceptions/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'mlk'
                            name 'Michael Lee'
                            email 'pom@michael-lloyd-lee.me.uk'
                        }
                        developer {
                            id 'fergusstrange'
                            name 'Fergus Strange'
                        }
                        developer {
                            id 'steinfletcher'
                            name 'Stein Fletcher'
                        }
                    }
                }
            }
        }
    }
}
